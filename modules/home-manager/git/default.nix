{
  lib,
  config,
  pkgs,
  inputs,
  inputs',
  ...
}:
{
  home.packages = with pkgs; [
    act
    gist
    gitflow
  ];

  programs = {
    git = {
      enable = true;
      userName = "sh-koh";
      userEmail = "70974710+sh-koh@users.noreply.github.com";
      package = pkgs.gitFull;
      lfs.enable = true;
      delta = {
        enable = true;
        options = {
          syntax-theme = "base16-stylix";
          dark = true;
          navigate = true;
          line-numbers = true;
          hyperlinks = true;
          side-by-side = true;
          color-moved = "default";
        };
      };
      hooks = {
        prepare-commit-msg = lib.getExe (
          pkgs.writeShellApplication {
            name = "prepare-commit-msg-hook.sh";
            runtimeInputs = with pkgs; [ ];
            text = ''
              cat << 'EOF' >> "$1"
              # Commit types:
              # [ğŸ—‘ï¸] clean(_): cleaned project.
              # [âœ¨] feat(_): added a very cool feature !
              # [ğŸ‰] init(_): hello world !
              # [âœï¸] norm(_): normed project.
              # [ğŸš§] test(_): testing things, might broke.
              # [ğŸ—ï¸] wip(_): work in progress, not done yet.
              # [ğŸ”¨] fix(_): fixed some things.
              # [ğŸ“] doc(_): added documentation.
              # [â„ï¸] nix(_): nix related changes.
              # [ğŸ”„] revert(_): reverted some changes.
              # [ğŸ’¡] update(_): updated!
              EOF
            '';
          }
        );
      };
      extraConfig = {
        init.defaultBranch = "master";
        push.autoSetupRemote = true;
        pull.rebase = true;
        url = lib.concatMapAttrs (k: _v: {
          "ssh://git@${k}.com/${config.programs.git.userName}".insteadOf =
            "https://${k}.com/${config.programs.git.userName}";
        }) (lib.genAttrs [ "github" "gitlab" ] (x: x));
      };
    };

    ssh = {
      matchBlocks =
        lib.genAttrs
          [
            "github"
            "gitlab"
          ]
          (host: {
            host = "${host}.com";
            user = "git";
            identityFile = "~/.ssh/id_${host}";
          });
    };
  };
}
